//   Made:By Kirlein (Tyirial Script Concept)  //
// Devil Square based off the mmorpg Muonline  //
// Have Fun and feel free to leave suggestions //
//          Leave This intacted                //
// +   Translated by Stillhard aka Dewa        //
// +  Add times for 24 hours of play :P        //
// +  Add a little bit infos for newbie users  //
//      Completely rewrite by ~AnnieRuru~      //
// +  Tested with SVN10900 Stable              //
/////////////////////////////////////////////////

niflheim,22,158,5	script	Devil Square Guardian	459,{
	mes "[Devil Square Guardian]";
	mes "Welcome to the ^FF0000Devil Square^000000's entrance.";
	mes "If you failed to survive this ordeal, the door will be closed. You will have to wait for it to open if you want to try again.";
	next;
	menu "Enter",-,"Info",L_info,"Cancel",L_cancel;
	
	if ( $@DS_entry == 0 ) {
		for (set .@b, 0; .@b < 100; set .@b, .@b + 1) {
			if ($@DS_Rejoin[.@b] == getcharid(0)) {
				set $@DS_Rejoin[.@b], 0;
				if (gettimetick(2) > DS_Enter_sec + 180) {
					mes "[Devil Square Guardian]";
					mes "You can only rejoin DS within 3 minutes of leaving.";
					close;					
				}
				percentheal 100,100;
				mapannounce "ordeal_1-1", "Devil Square : "+ strcharinfo(0) +" has re-entered the event",0;
				mapannounce "prontera", "Devil Square : "+ strcharinfo(0) +" has re-entered the event",0;
				warp "ordeal_1-1",183,182;
				end;
			}
			else if (.@b == 99)
					goto L_error;
			else
				continue;
		}
	}
	if ( Baselevel < 90 || Zeny < 20000 || Class == 0 || Class == 4001) goto L_req;
	if ( getarraysize($@DS_Rejoin) >= 100 ) goto L_toomany;
	for (set .@b, 0; .@b < 100; set .@b, .@b + 1) {
		if (!$@DS_Rejoin[.@b])
			set $@DS_Rejoin[.@b], getcharid(0);
		if ($@DS_Rejoin[.@b] == getcharid(0))
			break;
	}
	set Zeny,Zeny - 20000;
	mapannounce "prontera", "Devil Square : "+ strcharinfo(0) +" has entered the event",0;
	mapannounce "ordeal_1-1", "Devil Square : "+ strcharinfo(0) +" has entered the event",0;
	percentheal 100,100;
	addtimer 3000, strnpcinfo(0)+"::OnCheckMap";
	warp "ordeal_1-1",183,182;
	end;

L_error:
	mes "[Devil Square Guardian]";
	mes "Devil Square is closed";
	mes "It opens at ";
	mes "02:00, 06:00, 10:00, 14:00, 18:00 and 22:00 server time.";
	close;

L_req:
	mes "[Devil Square Guardian]";
	mes "Minimum base level required to enter is 90";
	mes "The registration fee is 20,000z";
	mes "Make sure you fullfill both requirements if you want to enter.";
	close;

L_toomany:
	mes "[Devil Square Guardian]";
	mes "I'm sorry, but Devil Square is FULL";
	mes "The maxmimum amount of challengers that can enter is 100 players.";
	close;

L_info:
	mes "[Devil Square Guardian]";
	mes "There are 3 levels in which you will encounter several monsters.";
	mes "In every level that you reach, the monster count will increase. Stronger monsters will also appear depending on the level.";
	mes "If you successfully cleared all 3 levels, you'll be rewarded with Gold Coins and other items!!";
	mes "The maximum amount of challengers that can enter are 100 players.";
	close;

L_cancel:
	mes "[Devil Square Guardian]";
	mes "See you!";
	close;

OnPCLogoutEvent:
	if (strcharinfo(3) == "ordeal_1-1")
		set DS_Enter_sec, gettimetick(2);
	end;

OnCheckMap:
	if (strcharinfo(3) != "ordeal_1-1") {
		set DS_Enter_sec, gettimetick(2);
	}
	else {
		addtimer 3000, strnpcinfo(0)+"::OnCheckMap";
	}
	end;
}

ordeal_1-1,0,0,0	script	DS_HiddenNpc	-1,{
OnWhisperGlobal:
    if (getgmlevel() < 99) end; //Put minimum GM level here
OnClock0200:
OnClock0600:
OnClock1000:
OnClock1400:
OnClock1800:
OnClock2200:
	set $@DS_complete,0;
	disablenpc "Exit#DS";
	mapannounce "ordeal_1-1","The Devil Square is now closed",0;
	killmonsterall "ordeal_1-1";
	for (set .@b, 0; .@b < 100; set .@b, .@b + 1) {
		set $@DS_Rejoin[.@b], 0;
	}
	announce "The Devil Square has opened! 5 minutes until it starts. @go 13 or warp to Niflheim to enter.",0;
	set $@DS_entry,1;
	sleep 60000;
	announce "Devil Square will begin in 4 minutes..!!",0;
	sleep 60000;
	announce "Devil Square will begin in 3 minutes..!!",0;
	sleep 60000;
	announce "Devil Square will begin in 2 minutes..!!",0;
	sleep 60000;
	announce "Devil Square will begin in 1 minute..!!",0;
	sleep 60000;
	announce "The Devil Square Challenge has begun!",0;
	initnpctimer;
	set $@DS_entry,0;
	set $@mob,90;
	areamonster "ordeal_1-1",183,182,246,244,"[DS] SOLDIER SKELETON",1028,10,"DS_HiddenNpc::Ondevildead";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] Bow Guardian",1830,10,"DS_HiddenNpc::Ondevildead";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] FLAME SKULL",1869,15,"DS_HiddenNpc::Ondevildead";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] ARCHER SKELETON",1016,20,"DS_HiddenNpc::Ondevildead";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] MINOROUS",1149,15,"DS_HiddenNpc::Ondevildead";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] NIGHTMARE",1061,20,"DS_HiddenNpc::Ondevildead";
	end;

Ondevildead:
	set $@mob,$@mob - 1;
	if($@mob == 25) mapannounce "ordeal_1-1","Devil Square : 25 mobs to enter the 2nd round",0;
	if($@mob == 5) mapannounce "ordeal_1-1","Devil Square : 5 mobs to enter 2nd round",0;
	if($@mob == 0) { 
		mapannounce "ordeal_1-1","Devil Square : You have cleared this stage. Prepare yourself for the next stage in....",0;
		sleep2 500;
		mapannounce "ordeal_1-1","Devil Square : 5",0;
		sleep2 5000;
		mapannounce "ordeal_1-1","Devil Square : 4",0;
		sleep2 4000;
		mapannounce "ordeal_1-1","Devil Square : 3",0;
		sleep2 3000;
		mapannounce "ordeal_1-1","Devil Square : 2",0;
		sleep2 2000;
		mapannounce "ordeal_1-1","Devil Square : 1...",0;
		sleep2 1000;
		mapannounce "ordeal_1-1","Devil Square : Next Stage!",0;
		sleep2 1000;
		goto Ondevil2;
	}
	end;

Ondevil2:
	set $@mob,95;
	areamonster "ordeal_1-1",183,182,246,244,"[DS] DARK ILLUSION",1302,20,"DS_HiddenNpc::Ondevildead2";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] SKOGUL",1761,10,"DS_HiddenNpc::Ondevildead2";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] MARIONETTE",1143,15,"DS_HiddenNpc::Ondevildead2";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] INJUSTICE",1257,15,"DS_HiddenNpc::Ondevildead2";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] Salamander",1831,10,"DS_HiddenNpc::Ondevildead2";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] FLAME SKULL",1869,25,"DS_HiddenNpc::Ondevildead2";
	end;

Ondevildead2:
	set $@mob,$@mob - 1;
	if($@mob == 25) mapannounce "ordeal_1-1","Devil Square : 25 mobs left to enter the 3rd round",0;
	if($@mob == 5) mapannounce "ordeal_1-1","Devil Square : 5 mobs left to enter the 3rd round",0;
	if($@mob == 0) {
		mapannounce "ordeal_1-1","Devil Square : You're now at the last stage... The final battle will begin in...",0;
		sleep2 500;
		mapannounce "ordeal_1-1","Devil Square : 5",0;
		sleep2 5000;
		mapannounce "ordeal_1-1","Devil Square : 4",0;
		sleep2 4000;
		mapannounce "ordeal_1-1","Devil Square : 3",0;
		sleep2 3000;
		mapannounce "ordeal_1-1","Devil Square : 2",0;
		sleep2 2000;
		mapannounce "ordeal_1-1","Devil Square : 1...",0;
		sleep2 1000;
		mapannounce "ordeal_1-1","Devil Square : Final stage!",0;
		sleep2 1000;
		goto Ondevil3;
	}
	end;

Ondevil3:
	set $@mob,91;
	areamonster "ordeal_1-1",183,182,246,244,"[DS] Salamander",1831,25,"DS_HiddenNpc::Ondevildead3";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] Bow Guardian",1830,20,"DS_HiddenNpc::Ondevildead3";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] DARK ILLUSION",1302,25,"DS_HiddenNpc::Ondevildead3";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] FLAME SKULL",1869,15,"DS_HiddenNpc::Ondevildead3";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] PIAMETTE",1947,5,"DS_HiddenNpc::Ondevildead3";
	areamonster "ordeal_1-1",183,182,246,244,"[DS] FINAL BOSS",2008,1,"DS_HiddenNpc::Ondevildead3";
	end;

Ondevildead3:
	set $@mob,$@mob - 1;
	if($@mob == 15) mapannounce "ordeal_1-1","Devil Square : 15 mobs left till victory!",0;
	if($@mob == 5) mapannounce "ordeal_1-1","Devil Square : 5 mobs left till victory!",0;
	if($@mob == 0) {
		sleep 3000;
		announce "Devil Square : Congratulations to all the players who have completed the challenge!",0;
		goto Ondevil4;
	}
	end;

Ondevil4:
	set $@DS_complete,1;
	for (set .@b, 0; .@b < 100; set .@b, .@b + 1) {
		set $@DS_Rejoin[.@b], 0;
	}
	if ($@mob == 0) enablenpc "Exit#DS";
	end;

OnTimer30000:
	if ( $@DS_complete == 1 ) {
		stopnpctimer;
		end;
	} else
	if (getmapusers("ordeal_1-1") == 0) {
		announce "All players failed to survive the Devil Square",0;
		killmonsterall "ordeal_1-1";
		stopnpctimer;
		end;
	} else {
		mapannounce "ordeal_1-1",getmapusers("ordeal_1-1") +" player" + ((getmapusers("ordeal_1-1")>1)?"s are":" is") +" still alive at the Devil Square...",0;
		initnpctimer;
		end;
	}

}

// -- Mapflags
ordeal_1-1	mapflag	nowarp
ordeal_1-1	mapflag	nowarpto
ordeal_1-1	mapflag	noteleport
ordeal_1-1	mapflag	nosave	SavePoint
ordeal_1-1	mapflag	nomemo
ordeal_1-1	mapflag	nobranch
ordeal_1-1	mapflag	nopenalty
ordeal_1-1	mapflag	monster_noteleport
ordeal_1-1	mapflag	kickafk

ordeal_1-1,246,245,3	script	Exit#DS	2008,{
	mes "[The Fallen]";
	mes "You have defeated me...";
	mes "Farewell...";
	next;
	mapannounce "prontera", "Devil Square : "+ strcharinfo(0) +" has completed the challenge.",0;
	mapannounce "ordeal_1-1", "Devil Square : "+ strcharinfo(0) +" has completed the challenge.",0;
	
	getitem 671, 1; 
	getitem 616, 2;
	if( rand(20) == 1 ){
		getitem 7086, 1;
	}
	warp "SavePoint",0,0;
	close;
}